* This do file is written to compute the regional "delta_omega"
* 
* Use IO table to compute the industrial foreign usage shares
* and then apply the Bartik form to get regional foreign usage share

* Years: 1990-1997 2002-2006; 1900-2006

* Written by: Yawen Liang
* Last updated: Oct 17th, 2015
*******************************************************************************************************************

clear
set more off
cd "C:\PhD Study\Year 3\Projects\Job Porlarization\Arranged Files"

** 1. Compute the industrial export shares

use "C:\PhD Study\Year 3\Projects\Job Porlarization\data\Import_IO\LFSind_IOind.dta",clear
* see sheet1 in "LFSind_ISIC3.xlxs" for full correspondence
replace LFSind = 11 if LFSind==12
replace LFSind = 11 if LFSind==13
replace LFSind = 11 if LFSind==14
replace LFSind = 16 if LFSind==17
replace LFSind = 16 if LFSind==18
replace LFSind = 27 if LFSind==24
replace LFSind = 27 if LFSind==25
replace LFSind = 27 if LFSind==26
replace LFSind = 41 if LFSind==42
replace LFSind = 75 if LFSind==52
replace LFSind = 63 if LFSind==64
duplicates drop LFSind IOind,force
duplicates tag IOind, gen(dupIOind)
replace dupIOind = dupIOind+1
save "data\temp\LFSind_IOind_Oct17.dta",replace

use "data\input\Mimp_IOind.dta",clear /* generated by Mimport_LFSind.do */
egen imp = rowtotal(c1imp-c42imp)
gen exp = c44dom
drop if totydom==0
gen nx = exp-imp
gen shr_nx = nx/totydom
gen shr_exp = c44dom/totydom

keep if year==1995|year==2002
keep IOind year shr_exp shr_nx nx exp imp totydom
joinby IOind using "data\temp\LFSind_IOind_Oct17.dta"
replace nx = nx/dupIOind
replace exp = exp/dupIOind
replace imp = imp/dupIOind
replace totydom = totydom/dupIOind
collapse (mean) shr_exp shr_nx (sum) exp imp nx totydom (first) LFSind_note, by(year LFSind)

replace year=1990 if year==1995 /* due to data limitation, use 1995 IO result to proxy begining of 1990-1997 period" */
*expand 2, gen(dexp)
*replace year=1997 if year==1990 & dexp==0
*replace year=2006 if year==2002 & dexp==0
*drop dexp

* use the nx_shr computed by UNcomtrade and UNIDO data for 1990 manufacture industries
merge 1:1 LFSind year using "data\output\UNIDOnx_shr_Oct25.dta"

replace shr_nx = shr_nx_UNIDO if _merge==3
drop _merge shr_nx_UNIDO

**2. compute the regional export shr using bartik form

* drop agriculture industries
drop if LFSind>=11 & LFSind<=18
merge 1:m LFSind year using  "data\temp\taskind_shr_kab.dta" /* generated by Bartik_15Oct25.do */
keep if year==1990|year==2002
*keep if year==1990
tab _merge
* onlye LFS=95, 96 have _merge=2, it is service industry
* service industry is not included in IO table, use export share=0 for them
drop _merge

* set foreign usage share to be zero if we don't have the trade data
merge m:1 LFSind year using "data\temp\WITS_Mimp_devLFS979806_dreshape.dta" /* from Bartik_15Oct25.do */
replace shr_exp = 0 if _merge==1
replace shr_nx = 0 if _merge==1
replace nx = 0 if _merge==1
drop if _merge==2

gen nxWLD = expWLD- impWLD

* keep the industries with same signs from two data source
gen test = nx* nxWLD>=0
drop if test==0&_merge==3
drop _merge

foreach task in a r m{
gen delta_`task'_kab = `task'ind_shr_kab*shr_nx
gen delta_`task'_kab1990 = `task'ind_shr_kab_1990*shr_nx
gen nx_`task'_kab = `task'ind_shr_kab*nx
gen nx_`task'_kab1990 = `task'ind_shr_kab_1990*nx
gen toty_`task'_kab=`task'ind_shr_kab*totydom
gen toty_`task'_kab1990=`task'ind_shr_kab_1990*totydom
}
collapse (sum) delta_*_kab delta_*_kab1990 nx_*_kab* toty_*_kab*, by(code1990 year)

replace year=1996 if year==1990
replace year=2006 if year==2002

foreach task in a r m{
replace delta_`task'_kab = nx_`task'_kab/toty_`task'_kab
replace delta_`task'_kab1990 = nx_`task'_kab1990/toty_`task'_kab1990
}

save "data\output\kabexp_shr_Oct26.dta",replace


